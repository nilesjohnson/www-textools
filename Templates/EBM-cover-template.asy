// A template file for creating Covers for use with the Espresso Book Machine
// Try to make the title look similar to the title generated by the wwwebm.cls file
//  (which is the same styling as the wwwlnb.cls and the wwwnotes2.cls at the moment)

usepackage("kpfonts");

// Fix unit size to be inches!!
unitsize(1inch);

// Book size
//pair booksize = (7.4,9.5);
pair booksize = (7.36,10.41);
real num_pages = 463; //store as real since we need to divide

// Compute some sizes
real PPI = 434; // fixed by EBM, don't change
pair spinesize = ( num_pages/PPI, booksize.y );
pair totalsize = ( booksize.x * 2 + spinesize.x, booksize.y);

// Requested page boundaries.
pair page_min = (0, 0);
pair page_max = (17, 11);

// Define some coordinates
pair botleftback = (page_max - totalsize) / 2;
pair botleftspine = botleftback + (booksize.x,0);
pair botleftfront = botleftspine + (spinesize.x,0);

// Draw background gradient
pen[] p={deepgreen, paleblue, orange, white};
tensorshade(box(page_min,page_max),p);

// Draw guides (remove when done)
//draw(shift(botleftback)*box((0,0),booksize), blue+5);
//draw(shift(botleftfront)*box((0,0),booksize), green+5);
//draw(shift(botleftspine)*box((0,0),spinesize), red+5);


// Draw front page
label(scale(3.3)*"\textsc{Research Notes}",shift(botleftfront)*(0.7,8),E);
label(scale(1.8)*"2011 December -- 2015
May",shift(botleftfront)*(0.7,6),E);
label(scale(1.8)*"\'Ecole Polytechnique F\'ed\'erale de Lausanne",
shift(botleftfront)*(0.7,5.5),E);
label(scale(2.7)*"Willie Wai Yeung Wong",
shift(botleftfront)*(6.8,2.5),W);

// Draw spine
label(rotate(270)*scale(2.3)*"\textsc{Wong \quad \textbullet \quad Notes:
2011.12 -- 2015.5 \quad \textbullet \quad EPFL}", (8.5,5.5));

// Fix page size: solution found on TeX.SE
//    http://tex.stackexchange.com/a/287018/119
// Clip the elements in the current picture. Prevents the page from being enlarged and needs to be called after all drawing commands.
clip(box(page_min, page_max));
// Set the page size. Prevents the page from being shrunken.
fixedscaling(page_min, page_max);
